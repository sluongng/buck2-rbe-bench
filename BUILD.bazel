SIZE_RANGE = range(0, 1000)

XYZ_SHA256 = "72036ae48c55d632cc333cac04c78be336b0c76f39b225d023e218efe077ab1b"

# Add some execution time to each action to simulate a real build.
SLEEP_CMD = "sleep $$((RANDOM % 5));"

[
    genrule(
        name = "x_{}".format(n),
        outs = ["x_{}.txt".format(n)],
        cmd = SLEEP_CMD + "echo x_{} > $@".format(n),
    )
    for n in SIZE_RANGE
]

[
    genrule(
        name = "y_{}".format(n),
        outs = ["y_{}.txt".format(n)],
        cmd = SLEEP_CMD + "echo y_{} > $@".format(n),
    )
    for n in SIZE_RANGE
]

[
    genrule(
        name = "z_{}".format(n),
        srcs = [
            ":x_{}".format(n),
            ":y_{}".format(n),
        ],
        outs = ["z_{}.txt".format(n)],
        cmd = SLEEP_CMD + "cat $(location :x_{n}) $(location :y_{n}) > $@".format(n = n),
    )
    for n in SIZE_RANGE
]

genrule(
    name = "xyz",
    srcs = [
        ":z_{}".format(n)
        for n in SIZE_RANGE
    ],
    outs = ["xyz.txt"],
    # cat $(location :z_0) $(location :z_1) ... $(location :z_999) > $@,
    cmd = SLEEP_CMD + "cat " + " ".join([
        "$(location :z_{})".format(n)
        for n in SIZE_RANGE
    ]) + " > $@",
)

sh_test(
    name = "test",
    srcs = ["test.sh"],
    args = [
        "$(location :xyz)",
        XYZ_SHA256,
    ],
    data = [
        ":xyz",
    ],
)
